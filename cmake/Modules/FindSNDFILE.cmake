#  SNDFILE_INCLUDE_DIRS   - where to find sndfile.h
#  SNDFILE_LIBRARIES      - List of libraries when using sndfile
#  SNDFILE_FOUND          - True if sndfile found


# If cross-compiling, also include the Ogg, FLAC and Vorbis libs
if(CROSS_MINGW)
  FIND_PATH(SNDFILE_INCLUDE_DIR NAMES sndfile.h PATHS ${CROSS_DEPS}/include NO_DEFAULT_PATH)
  MARK_AS_ADVANCED(SNDFILE_INCLUDE_DIR)
  FIND_LIBRARY(SNDFILE_LIBRARY NAMES sndfile PATHS ${CROSS_DEPS}/lib NO_DEFAULT_PATH)
  FIND_LIBRARY(VORBIS_LIBRARY NAMES vorbis PATHS ${CROSS_DEPS}/lib NO_DEFAULT_PATH)
  FIND_LIBRARY(VORBISENC_LIBRARY NAMES vorbisenc PATHS ${CROSS_DEPS}/lib NO_DEFAULT_PATH)
  FIND_LIBRARY(VORBISFILE_LIBRARY NAMES vorbisfile PATHS ${CROSS_DEPS}/lib NO_DEFAULT_PATH)
  FIND_LIBRARY(OGG_LIBRARY NAMES ogg PATHS ${CROSS_DEPS}/lib NO_DEFAULT_PATH)
  FIND_LIBRARY(FLAC_LIBRARY NAMES FLAC PATHS ${CROSS_DEPS}/lib NO_DEFAULT_PATH)
  MARK_AS_ADVANCED(SNDFILE_LIBRARY VORBIS_LIBRARY VORBISENC_LIBRARY VORBISFILE_LIBRARY OGG_LIBRARY FLAC_LIBRARY)
  SET(_extravars VORBIS_LIBRARY VORBISENC_LIBRARY VORBISFILE_LIBRARY OGG_LIBRARY FLAC_LIBRARY)
  SET(_extralibs ${VORBIS_LIBRARY} ${VORBISENC_LIBRARY} ${VORBISFILE_LIBRARY} ${OGG_LIBRARY} ${FLAC_LIBRARY})
else()
  FIND_PATH(SNDFILE_INCLUDE_DIR NAMES sndfile.h)
  MARK_AS_ADVANCED(SNDFILE_INCLUDE_DIR)
  FIND_LIBRARY(SNDFILE_LIBRARY NAMES sndfile)
  MARK_AS_ADVANCED(SNDFILE_LIBRARY)
  SET(_extravars)
  SET(_extralibs)
endif(CROSS_MINGW)

# handle the QUIETLY and REQUIRED arguments and set SNDFILE_FOUND to 
# TRUE if all listed variables are TRUE
include(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(SNDFILE
                                  REQUIRED_VARS SNDFILE_LIBRARY ${_extravars}
                                  SNDFILE_INCLUDE_DIR )

IF(SNDFILE_FOUND)
  SET(SNDFILE_LIBRARIES ${SNDFILE_LIBRARY} ${_extralibs})
  SET(SNDFILE_INCLUDE_DIRS ${SNDFILE_INCLUDE_DIR})
ENDIF(SNDFILE_FOUND)
